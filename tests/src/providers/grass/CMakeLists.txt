INCLUDE_DIRECTORIES(
  ${CMAKE_SOURCE_DIR}/src/providers/grass
)
INCLUDE_DIRECTORIES(SYSTEM
  ${GDAL_INCLUDE_DIR}
  ${PROJ_INCLUDE_DIR}
  ${GEOS_INCLUDE_DIR}
  ${POSTGRES_INCLUDE_DIR}
)


MACRO (ADD_QGIS_GRASS_TEST testname testsrc)
  SET(qgis_${testname}_SRCS ${testsrc} ${util_SRCS})
  ADD_CUSTOM_TARGET(qgis_${testname}moc ALL DEPENDS ${qgis_${testname}_MOC_SRCS})
  ADD_EXECUTABLE(qgis_${testname} ${qgis_${testname}_SRCS})

  INCLUDE_DIRECTORIES(
    ${CMAKE_BINARY_DIR}/src/providers/grass/
  )

  SET_TARGET_PROPERTIES(qgis_${testname} PROPERTIES
    AUTOMOC TRUE
    COMPILE_FLAGS "-DGRASS_BASE=\\\"${GRASS_PREFIX}\\\" \"-DGRASS_BUILD_VERSION=\""
  )
  TARGET_LINK_LIBRARIES(qgis_${testname}
    ${QT_QTXML_LIBRARY}
    ${QT_QTCORE_LIBRARY}
    ${QT_QTSVG_LIBRARY}
    ${QT_QTTEST_LIBRARY}
    ${PROJ_LIBRARY}
    ${GEOS_LIBRARY}
    ${GDAL_LIBRARY}
    qgis_core
    qgisgrass
  )
  IF(WIN32 AND NOT USING_NMAKE)
    FILE(WRITE ${CMAKE_CURRENT_BINARY_DIR}/${testname}.cmake
"
SET(ENV{PATH} \"${GRASS_PREFIX}/bin;${GRASS_PREFIX}/lib;${CMAKE_BINARY_DIR}/output/bin/\${CMAKE_BUILD_TYPE};${CMAKE_BINARY_DIR}/output/plugins/\${CMAKE_BUILD_TYPE};\$ENV{PATH}\")
SET(ENV{GISBASE} \"${GRASS_PREFIX}\")
MESSAGE(STATUS \"Running ${CMAKE_BINARY_DIR}/output/bin/\${CMAKE_BUILD_TYPE}/qgis_${testname}\")
MESSAGE(STATUS \"PATH:\$ENV{PATH}\")
MESSAGE(STATUS \"GISBASE:\$ENV{GISBASE}\")
EXECUTE_PROCESS(
  COMMAND ${CMAKE_BINARY_DIR}/output/bin/\${CMAKE_BUILD_TYPE}/qgis_${testname}
  RESULT_VARIABLE import_res
)
IF(import_res)
  MESSAGE(FATAL_ERROR \"Test failed: \${import_res}\")
ENDIF(import_res)
"
)
    ADD_TEST(NAME qgis_${testname} COMMAND ${CMAKE_COMMAND} -D CMAKE_BUILD_TYPE=$<CONFIGURATION> -P ${CMAKE_CURRENT_BINARY_DIR}/${testname}.cmake)
  ELSE (WIN32 AND NOT USING_NMAKE)
    ADD_TEST(qgis_${testname} ${CMAKE_BINARY_DIR}/output/bin/qgis_${testname} -maxwarnings 10000)
  ENDIF (WIN32 AND NOT USING_NMAKE)

  IF(WIN32)
    SET_PROPERTY(TEST qgis_${testname} PROPERTY ENVIRONMENT "GISBASE=${GRASS_PREFIX}")
    IF (USING_NMAKE)
      SET_PROPERTY(TEST qgis_${testname} PROPERTY
        ENVIRONMENT "PATH=${GRASS_PREFIX}/lib;${CMAKE_BINARY_DIR}/output/bin;${CMAKE_BINARY_DIR}/output/plugins;$ENV{PATH}"
      )
    ENDIF (USING_NMAKE)
  ELSE(WIN32)
    IF (APPLE)
      SET_PROPERTY(TEST qgis_${testname} PROPERTY
        ENVIRONMENT "DYLD_LIBRARY_PATH=${GRASS_PREFIX}/lib:$ENV{DYLD_LIBRARY_PATH}"
      )
    ELSE (APPLE)
      # UNIX
      SET_PROPERTY(TEST qgis_${testname} PROPERTY
        ENVIRONMENT "LD_LIBRARY_PATH=${GRASS_PREFIX}/lib:$ENV{LD_LIBRARY_PATH}"
      )
    ENDIF (APPLE)
  ENDIF (WIN32)
ENDMACRO (ADD_QGIS_GRASS_TEST)

INCLUDE_DIRECTORIES(${GRASS_INCLUDE_DIR} ${CMAKE_CURRENT_BINARY_DIR})
ADD_QGIS_GRASS_TEST(grassprovidertest testqgsgrassprovider.cpp)
IF(MSVC)
  # 4611 setjmp
  SET_SOURCE_FILES_PROPERTIES(testqgsgrassprovider.cpp PROPERTIES COMPILE_FLAGS "-wd4611")
ENDIF(MSVC)


