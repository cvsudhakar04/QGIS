SET (DB_MANAGER_PLUGIN_DIR ${QGIS_DATA_DIR}/python/plugins/db_manager)
SET (DB_MANAGER_PLUGIN_OUTPUT_DIR ${PYTHON_OUTPUT_DIRECTORY}/plugins/db_manager)

ADD_SUBDIRECTORY(db_plugins)
ADD_SUBDIRECTORY(icons)

FILE(GLOB OTHER_FILES LICENCE README TODO)
FILE(GLOB PY_FILES *.py)

FILE(GLOB UI_FILES ui/*.ui)
PYQT4_WRAP_UI(PYUI_FILES ${UI_FILES})
PYQT4_ADD_RESOURCES(PYRC_FILES resources.qrc)

ADD_CUSTOM_TARGET(db_manager ALL DEPENDS ${PYUI_FILES} ${PYRC_FILES})

INSTALL(FILES ${OTHER_FILES} DESTINATION ${DB_MANAGER_PLUGIN_DIR})
INSTALL(FILES ${PY_FILES} DESTINATION ${DB_MANAGER_PLUGIN_DIR})
INSTALL(FILES ui/__init__.py ${PYUI_FILES} DESTINATION ${DB_MANAGER_PLUGIN_DIR}/ui)
INSTALL(FILES ${PYRC_FILES} DESTINATION ${DB_MANAGER_PLUGIN_DIR})

# Stage install

FOREACH(file ${PYUI_FILES} ui/__init__.py)
  ADD_CUSTOM_COMMAND(TARGET db_manager
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory ${DB_MANAGER_PLUGIN_OUTPUT_DIR}/ui
    COMMAND ${CMAKE_COMMAND} -E copy ${file} ${DB_MANAGER_PLUGIN_OUTPUT_DIR}/ui
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  )
ENDFOREACH(file)

FOREACH(file ${PY_FILES} ${PYRC_FILES} ${OTHER_FILES})
  ADD_CUSTOM_COMMAND(TARGET db_manager
    POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory ${DB_MANAGER_PLUGIN_OUTPUT_DIR}
    COMMAND ${CMAKE_COMMAND} -E copy ${file} ${DB_MANAGER_PLUGIN_OUTPUT_DIR}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  )
ENDFOREACH(file)


